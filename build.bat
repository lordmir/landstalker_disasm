@ECHO OFF
SETLOCAL ENABLEEXTENSIONS ENABLEDELAYEDEXPANSION

SET STANDARD_BUILDOPTS="/p /d /o ae-,e+,w+,c+,op+,os+,ow+,oz+,l_"
SET AS="tools\build\asm68k.exe"
SET Z80AS="tools\build\asw\asw.exe"
SET Z80AS_OPTS=""
SET Z80LINK="tools\build\asw\p2bin.exe"
SET Z80LINK_OPTS="-l 0xff -r 0x0000-0x1f7f -k"
SET Z80SRC="code/audio/sounddrv.asm"
SET Z80OBJ="cube.p"
SET Z80BIN="cube.bin"
SET REGION=US
SET EXPANDED=0
SET MANUAL_OUTPUT=

:PARSE
IF "%~1"=="-r" (
    SET REGION=%2
    SHIFT
    SHIFT
    GOTO PARSE
)
IF "%~1"=="-e" (
    SET /A EXPANDED=1
    SHIFT
    GOTO PARSE    
)
IF "%~1"=="-o" (
    SET MANUAL_OUTPUT=%~2
    SHIFT
    SHIFT
    GOTO PARSE    
)
IF "%~1"=="" (
    GOTO ENDPARSE
)
ECHO Usage: %~0% [-r ^(US^|JP^|EUR^)] [-e] [-o <Output ROM Filename>]
EXIT 1

:ENDPARSE

SET SOURCE="landstalker_us.asm"
SET OUTPUT="landstalker_us.bin"
SET SYMBOL="landstalker_us.sym"
SET LISTING="landstalker_us.lst"
SET /A REGION_CODE=0
SET /A NTSC=1
SET /A ENABLE_REGION_CHECK=1
SET /A FIX_COLLISION_GLITCH=1
SET /A FIX_ARMLET_SKIP=1
SET /A FIX_EINSTEIN_WHISTLE_FLAG_CHECK=1
SET /A FIX_SPRITE_HIDE_BUG=1
SET /A ENABLE_GOLD_COUNTUP_ON_TREASURE=1
SET /A FIX_POTENTIAL_CORRUPTION_ON_GOLA=1
SET /A FIX_GOLD_CAP_ON_FILE_LOAD=1
SET /A FIX_END_CREDITS_GLITCH=0

:: Options
IF %REGION%==US (
    IF %EXPANDED%==1 (
        SET SOURCE="landstalker_us_expanded.asm"
        SET OUTPUT="landstalker_us_expanded.bin"
        SET SYMBOL="landstalker_us_expanded.sym"
        SET LISTING="landstalker_us_expanded.lst"
    )
    SET /A EXPANDED=%EXPANDED%
) ELSE IF %REGION%==JP (
    IF %EXPANDED%==1 (
        SET SOURCE="landstalker_jp_expanded.asm"
        SET OUTPUT="landstalker_jp_expanded.bin"
        SET SYMBOL="landstalker_jp_expanded.sym"
        SET LISTING="landstalker_jp_expanded.lst"
        SET /A EXPANDED=1
    ) ELSE (
        SET SOURCE="landstalker_jp.asm"
        SET OUTPUT="landstalker_jp.bin"
        SET SYMBOL="landstalker_jp.sym"
        SET LISTING="landstalker_jp.lst"
        SET /A EXPANDED=0
    )
    SET /A REGION_CODE=1
    SET /A NTSC=1
    SET /A ENABLE_REGION_CHECK=0
    SET /A FIX_COLLISION_GLITCH=0
    SET /A FIX_ARMLET_SKIP=0
    SET /A FIX_EINSTEIN_WHISTLE_FLAG_CHECK=0
    SET /A FIX_SPRITE_HIDE_BUG=0
    SET /A ENABLE_GOLD_COUNTUP_ON_TREASURE=0
    SET /A FIX_POTENTIAL_CORRUPTION_ON_GOLA=0
    SET /A FIX_GOLD_CAP_ON_FILE_LOAD=0
    SET /A FIX_END_CREDITS_GLITCH=0
) ELSE IF %REGION%==EUR (
    IF %EXPANDED%==1 (
        SET SOURCE="landstalker_eur_expanded.asm"
        SET OUTPUT="landstalker_eur_expanded.bin"
        SET SYMBOL="landstalker_eur_expanded.sym"
        SET LISTING="landstalker_eur_expanded.lst"
        SET /A EXPANDED=1
    ) ELSE (
        SET SOURCE="landstalker_eur.asm"
        SET OUTPUT="landstalker_eur.bin"
        SET SYMBOL="landstalker_eur.sym"
        SET LISTING="landstalker_eur.lst"
        SET /A EXPANDED=0
    )
    SET /A REGION_CODE=2
    SET /A NTSC=0
    SET /A ENABLE_REGION_CHECK=1
    SET /A FIX_COLLISION_GLITCH=1
    SET /A FIX_ARMLET_SKIP=1
    SET /A FIX_EINSTEIN_WHISTLE_FLAG_CHECK=1
    SET /A FIX_SPRITE_HIDE_BUG=1
    SET /A ENABLE_GOLD_COUNTUP_ON_TREASURE=1
    SET /A FIX_POTENTIAL_CORRUPTION_ON_GOLA=1
    SET /A FIX_GOLD_CAP_ON_FILE_LOAD=1
    SET /A FIX_END_CREDITS_GLITCH=1
) ELSE (
    ECHO Unsupported Region
    EXIT 1
)

SET DEFINES="/e "
SET DEFINES=%DEFINES%"REGION="%REGION_CODE%
SET DEFINES=%DEFINES%";NTSC="%NTSC%
SET DEFINES=%DEFINES%";EXPANDED="%EXPANDED%
SET DEFINES=%DEFINES%";ENABLE_REGION_CHECK="%ENABLE_REGION_CHECK%
SET DEFINES=%DEFINES%";FIX_COLLISION_GLITCH="%FIX_COLLISION_GLITCH%
SET DEFINES=%DEFINES%";FIX_ARMLET_SKIP="%FIX_ARMLET_SKIP%
SET DEFINES=%DEFINES%";FIX_WHISTLE_CHECK="%FIX_EINSTEIN_WHISTLE_FLAG_CHECK%
SET DEFINES=%DEFINES%";FIX_SPRITE_HIDE_BUG="%FIX_SPRITE_HIDE_BUG%
SET DEFINES=%DEFINES%";ENABLE_GOLD_COUNT_AT_END="%ENABLE_GOLD_COUNTUP_ON_TREASURE%
SET DEFINES=%DEFINES%";FIX_GOLA_BUG="%FIX_POTENTIAL_CORRUPTION_ON_GOLA%
SET DEFINES=%DEFINES%";FIX_GOLD_CAP="%FIX_GOLD_CAP_ON_FILE_LOAD%
SET DEFINES=%DEFINES%";FIX_END_CREDITS="%FIX_END_CREDITS_GLITCH%
SET DEFINES=%DEFINES:"=%

SET Z80DEFINES="-D "
SET Z80DEFINES=%Z80DEFINES%"EXPANDED="%EXPANDED%
SET Z80DEFINES=%Z80DEFINES:"=%

SET Z80AS_OPTS=%Z80AS_OPTS:"=%
SET Z80LINK_OPTS=%Z80LINK_OPTS:"=%

IF NOT "%MANUAL_OUTPUT%"=="" SET OUTPUT=%MANUAL_OUTPUT%

@ECHO ON
%Z80AS% %Z80SRC% %Z80AS_OPTS% -o %Z80OBJ% %Z80DEFINES%
%Z80LINK% %Z80OBJ% %Z80BIN% %Z80LINK_OPTS%
%AS% %STANDARD_BUILDOPTS% %DEFINES% %SOURCE%,%OUTPUT%,%SYMBOL%,%LISTING%
python tools\build\fix_checksum.py %OUTPUT%

@ECHO OFF
ENDLOCAL
